func rand()
    var now = string(time.now())
    return round(number(now [now [len] 1 -]))
end func

func round(number)
    var number = string(number)
    var index = 0
    var result = ""
    while index number [len] <
        if number [index] "." =
            return number(result)
        end if
        var result = result number [index] +
        var index = index 1 +
    end while
    return number(result)
end func

func prime(ptr)
    #素数かどうか判定する関数
    #引数はポインタ
    
    if access(ptr) 2 <
        return false
    end if　#2未満は素数ではない

    if access(ptr) 4 <
        return true
    end if　#2以上4未満は素数

    var count = 2
    while count access(ptr) 0.5 ^ 1 + <
        if access(ptr) count % 0 =
            return false
        end if #割れるか確かめる
        var count = count 1 +
    end while

    return true #戻り値は論理型
end func

func sort(array)
    var i = 0
    while i array [len] <
        var j = i 1 +
        while j array [len] <
            if array [i] array [j] >
                var temp = array [i]
                var array [i] = array [j]
                var array [j] = temp
            end if
            var j = j 1 +
        end while
        var i = i 1 +
    end while
    return array
end func 

func split(word, key)
    var index = 0
    var result = list("")
    var level = 0
    while index word [len] <
        if word [index] key =
            var level = level 1 +
            var result = result list("") +
            var index = index 1 +
            next
        end if
        var result [level] = result [level] word [index] +
        var index = index 1 +
    end while
    return result
end func 